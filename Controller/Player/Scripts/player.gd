class_name Player
extends CharacterBody3D

@export var MOUSE_SENSITIVITY: float = 0.5
@export var TILT_LOWER_LIMIT := deg_to_rad(-90)
@export var TILT_UPPER_LIMIT := deg_to_rad(90)
@export var CAMERA_CONTROLLER: Camera3D
@export var LEAN_INTENSITY: float = 3.0

@export var ANIMATION_PLAYER: AnimationPlayer
@export var CROUCH_SHAPECAST: Node3D

#@export var WEAPON_CONTROLLER: WeaponController
@export var gravity: float = 10.0

var _mouse_input: bool = false
var _mouse_rotation: Vector3
var _rotation_input: float
var _tilt_input: float
var _player_rotation: Vector3
var _camera_rotation: Vector3

# Camera tilt system for wall run
var camera_tilt_target: float = 0.0
var camera_tilt_current: float = 0.0
@export var CAMERA_TILT_SPEED: float = 5.0

var _current_rotation: float

func _input(event: InputEvent) -> void:
	if event.is_action_pressed("exit"):
		get_tree().quit()
	
	
func _unhandled_input(event: InputEvent) -> void:
	_mouse_input = event is InputEventMouseMotion and Input.get_mouse_mode() == Input.MOUSE_MODE_CAPTURED	
	if _mouse_input:
		_rotation_input = -event.relative.x * MOUSE_SENSITIVITY
		_tilt_input = -event.relative.y * MOUSE_SENSITIVITY
		


func _update_camera(delta: float) -> void:
	_current_rotation = _rotation_input
	_mouse_rotation.x += _tilt_input * delta
	_mouse_rotation.x = clamp(_mouse_rotation.x, TILT_LOWER_LIMIT, TILT_UPPER_LIMIT)
	_mouse_rotation.y += _rotation_input * delta

	_player_rotation = Vector3(0.0,_mouse_rotation.y, 0.0)
	_camera_rotation = Vector3(_mouse_rotation.x, 0.0, 0.0)

	camera_tilt_current = lerp(camera_tilt_current, camera_tilt_target, CAMERA_TILT_SPEED * delta)

	CAMERA_CONTROLLER.transform.basis = Basis.from_euler(_camera_rotation)
	CAMERA_CONTROLLER.rotation.z = deg_to_rad(camera_tilt_current)

	global_transform.basis = Basis.from_euler(_player_rotation)
	_rotation_input = 0.0
	_tilt_input = 0.0

func set_camera_tilt(tilt_degrees: float) -> void:
	camera_tilt_target = tilt_degrees

func reset_camera_tilt() -> void:
	camera_tilt_target = 0.0

func _ready() -> void:
	global.player = self
	Input.mouse_mode = Input.MOUSE_MODE_CAPTURED
	CROUCH_SHAPECAST.add_exception(self)

func _physics_process(delta: float) -> void:
	global.debug.add_property("Player Velocity",snapped(velocity.length(),0.01),1)
	global.debug.add_property("Camera rotation",Vector2(_rotation_input,_tilt_input),2)	
	_update_camera(delta)

	move_and_slide()

func update_gravity(delta: float) -> void:
	velocity.y -= gravity * delta

func update_input(speed:float, acceleration:float, deceleration:float) -> void:
	var input_dir := Input.get_vector("move_left", "move_right", "move_forward", "move_backward")
	var direction := (transform.basis * Vector3(input_dir.x, 0, input_dir.y)).normalized()
	
	if input_dir.x != 0:
		set_camera_tilt(-input_dir.x * LEAN_INTENSITY)
	else:
		reset_camera_tilt()
	
	if direction:
		velocity.x = lerp(velocity.x,direction.x * speed, acceleration)
		velocity.z = lerp(velocity.z,direction.z * speed, acceleration)
	else:
		velocity.x = move_toward(velocity.x, 0, deceleration)
		velocity.z = move_toward(velocity.z, 0, deceleration)
		
func update_velocity()-> void:
	move_and_slide()
